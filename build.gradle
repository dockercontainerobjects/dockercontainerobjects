buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2' }
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${bintrayPluginVersion}"
        classpath "com.netflix.nebula:gradle-resolution-rules-plugin:${nebulaResolutionRulesPluginVersion}"
        classpath "com.netflix.nebula:nebula-project-plugin:${nebulaProjectPluginVersion}"
        classpath "com.netflix.nebula:nebula-release-plugin:${nebulaReleasePluginVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinPluginVersion}"
        classpath "org.junit.platform:junit-platform-gradle-plugin:${junitPlatformPluginVersion}"
    }
}

allprojects {
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'nebula.project'
    apply plugin: 'nebula.nebula-release'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'kotlin'
    apply plugin: 'nebula.maven-apache-license'
    apply plugin: 'nebula.resolution-rules'
    apply plugin: 'org.junit.platform.gradle.plugin'

    ext {
        projectId = rootProject.name
        projectName = rootProject.description

        githubOrgId = projectId
        githubProjectRepo = githubOrgId + '/' + projectId
        githubProjectUrl = 'https://github.com/' + githubProjectRepo
        githubProjectScmUrl = githubProjectUrl + '.git'
        githubProjectIssuesUrl = githubProjectUrl + '/issues'

        bintrayOrgId = projectId
        bintrayRepo = 'maven'
        bintrayName = project.name
    }

    contacts {
        'rivasdiaz@gmail.com' {
            moniker 'Ramon Rivas'
            github 'rivasdiaz'
            twitter 'rivasdiaz'
            roles ['committer']
        }
    }

    sourceCompatibility = rootProject.javaVersion
    targetCompatibility = rootProject.javaVersion

    repositories {
        jcenter()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            javaParameters = true
            jvmTarget = rootProject.javaVersion
        }
    }

    nebulaRelease {
        releaseBranchPatterns = [/master/, /HEAD/, /release\/v\d+\.\d+\.x/] as Set
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        publications = ['nebula']
        dryRun = false
        pkg {
            repo = project.bintrayRepo
            name = project.bintrayName
            userOrg = project.bintrayOrgId
            licenses = ['Apache-2.0']
            websiteUrl = project.githubProjectUrl
            vcsUrl = project.githubProjectScmUrl
            issueTrackerUrl = project.githubProjectIssuesUrl
            githubRepo = project.githubProjectRepo
            githubReleaseNotesFile = 'README.md'
            publicDownloadNumbers = true
            version {
                name = project.version
                released = new Date()
            }
        }
    }
}

dependencies {
    resolutionRules files("${rootProject.projectDir}/src/main/rules/exclude-log4j.json")
    resolutionRules files("${rootProject.projectDir}/src/main/rules/exclude-commons-logging.json")
    resolutionRules files("${rootProject.projectDir}/src/main/rules/upgrade-jackson.json")
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
